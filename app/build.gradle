apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {

    signingConfigs {
        sign {
            keyAlias 'nearpal'
            keyPassword '123456'
            storeFile file('F:/keystore/stanny.jks')
            storePassword '123456'
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    compileSdkVersion 28
    defaultConfig {
        applicationId "com.stanny.nearpal"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        // 添加一个默认渠道号
        manifestPlaceholders=[UMENG_CHANNEL_VALUE: "umeng"]
        flavorDimensions "versionCode"
        ndk {
            //设置支持的SO库架构
            abiFilters 'armeabi'//, 'armeabi-v7a', 'x86_64', 'arm64-v8a'
        }
        signingConfig signingConfigs.sign

        lintOptions {
            abortOnError false
        }

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath true
            }
        }
    }
    compileOptions {
        incremental = true  //开启增量编译
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    buildTypes {
        debug {
            crunchPngs false //关闭crunchPng优化, 以加快构建
            minifyEnabled false
            buildConfigField "boolean", "ISRELEASE", "false"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            buildConfigField "boolean", "ISRELEASE", "true"
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (!variant.buildType.debuggable) {
                outputFileName = "楮先生_" + variant.productFlavors[0].name + "_" + getTime() + "_" + defaultConfig.versionName + ".apk"
            }
        }
    }

    productFlavors {
        Tencent {
            //投放应用宝市场
        }
        XiaoMi {
            //投放小米市场
        }
        Vivo {
            //投放Vivo市场
        }
        Baidu {
            //投放百度市场
        }
        Oppo {
            //投放Oppo市场
        }
        HuaWei {
            //投放华为市场
        }
        WanDouJia {
            //投放豌豆荚市场
        }
    }

    productFlavors.all {
        flavor -> flavor.manifestPlaceholders = [UMENG_CHANNEL_VALUE : name]
    }
}

def getTime() {
    return new Date().format("yyMMdd", TimeZone.getTimeZone("UTC"))
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    implementation ('com.github.StannyBing:ZXUtils:2.3.0'){
        exclude group:'com.squareup.okhttp3'
    }
    implementation 'com.github.StannyBing:BingUIDemo:1.0.0'

    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.2'//其中latest.release指代最新版本号，也可以指定明确的版本号，例如1.2.0
    implementation 'com.tencent.bugly:nativecrashreport:3.7.1'//其中latest.release指代最新版本号，也可以指定明确的版本号，例如2.2.0
    implementation project(path: ':zxmvp')
    implementation 'me.jessyan:autosize:1.1.2'

    //PushSDK必须依赖基础组件库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:common:2.1.0'
    //PushSDK必须依赖utdid库，所以需要加入对应依赖
    implementation 'com.umeng.umsdk:utdid:1.1.5.3'
    //PushSDK
    implementation 'com.umeng.umsdk:push:6.0.1'
    //AnalyticsSDK
    implementation 'com.umeng.umsdk:analytics:8.0.0'

//    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.10'
}